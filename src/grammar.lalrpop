// vim: set syntax=rust:

use std::str::FromStr;
use crate::ast;

grammar;


pub Ident: ast::Ident = {
    r"[a-zA-Z_][0-9A-Za-z_]*" => ast::Ident(<>.to_string()),
};


pub Expr: ast::Expr = {
  Num,
  <ident:Ident> => ast::Expr::Ref(ident),
  FnCall,
  StringLiteral => ast::Expr::StringLiteral(<>),
}


FnCall: ast::Expr = {
  <fname:Ident> <args:ArgumentList> => ast::Expr::FnCall(fname, args)
}

pub ArgumentList: Vec<ast::Expr> = {
  "()" => Vec::new(),
  "(" <v:CommaSeparated<Expr>> ")" => v,
}



Num: ast::Expr = {
    r"\d+" => {
        let num = <>.to_string().replace("_", "");
        ast::Expr::Num(i32::from_str(<>).unwrap())
    },
    r"[0-9]+d[0-9_]+" => {
        let num = <>.to_string().replace("_", "").splitn(2, 'd').nth(1).unwrap().to_string();
        ast::Expr::Num(i32::from_str_radix(&num, 10).unwrap())
    },
    r"[0-9]+b[01_]+" => {
        let num = <>.to_string().replace("_", "").splitn(2, 'b').nth(1).unwrap().to_string();
        ast::Expr::Num(i32::from_str_radix(&num, 2).unwrap())
    },
    r"[0-9]+x[0-9a-fA-F_]+" => {
        let num = <>.to_string().replace("_", "").splitn(2, 'x').nth(1).unwrap().to_string();
        ast::Expr::Num(i32::from_str_radix(&num, 16).unwrap())
    },
};

StringLiteral: String = {
  r#""[^"]*""# => <>.to_string()
}


pub Func: ast::Func = {
  "fn" <fname:Ident> <pdecls:ParameterList> <rt:ReturnType?> "{"
  <stmts:StatementList> "}" => ast::Func {
    name: fname,
    params: pdecls,
    ret_type: rt,
    body: stmts,
  }
}

ParameterList: Vec<ast::ParamDecl> = {
  "()" => Vec::new(),
  "(" <pdecls:CommaSeparated<ParamDecl>> ")" => pdecls,
}


ReturnType: ast::Ident = {
  "->" <typ:Ident> => typ
}

pub StatementList: Vec<ast::Statement> = {
  <stmts:(Statement ";")*> => stmts.into_iter().map(|(stmt, s)| stmt).collect()
}

Statement: ast::Statement = {
  "()" => ast::Statement::Empty,
  <ident:Ident> "=" <e:Expr> => ast::Statement::LocalAssignment(ident, e),
}

CommaSeparated<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

ParamDecl: ast::ParamDecl = {
  <name:Ident> ":" <typ:Ident> => ast::ParamDecl {
    name: name,
    typ: typ
  }
}
